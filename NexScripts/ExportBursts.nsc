% Created by Daniel Vicarel on 9/4/14, 8:20 pm

% void ExportBursts()
% Scans through every file in a folder selected by the user
% Each file is burst analyzed with the provided algorithm, and the resulting burst timestamps are exported to text files
% 2016/05/26:  user now has option to open PLX or NEX files
% 2016/08/03:  Surprise algorithm now used by default
% 2017/05/22:  Cleaned up some global variable usage, and replaced MsgBox() calls with Trace()

Global filePath
Global ext

Global algorithm
Global minSurprise
Global maxISIToStart
Global minISIToEnd
Global minIBI
Global minBurstDur
Global minSpikesInBurst

NexOnTop()

% Remind future users/developers of how to make this script work properly
Trace("For this to work properly, make sure that:")
Trace("    NeuroExplorer is set to export Neuron (spike) and Interval data only! (must be reset every time NeuroExplorer is opened)")
Trace("    The Scrips directory (under Script > Script Options) is set appropriately")
Trace("    The Templates directory (under View > Options > Templates) is set appropriately")

% Let user choose data folder by selecting one of its files in a File Open dialog
Trace("")
Trace("Choose a data folder by selecting one of its files")
rootPath = SelectFile()
if (rootPath == "")
    Trace("No folder selected.")
    return
end

% Set path to the data folder
tempPath = rootPath
slashPos = Find(tempPath, "\")
while slashPos > 0
    tempPath = Mid(tempPath, slashPos + 1, StrLength(tempPath) - slashPos)
    slashPos = Find(tempPath, "\")
end
rootPath = Left(rootPath, Find(rootPath, tempPath) - 2)
Trace("Folder: " + rootPath)

% Ask the user which type of files they are trying to open
ext = "plx"
result = Dialog(GetActiveDocument(), ext, "Which files? (enter 'plx' or 'nex')", "string")
if (result <> 1)
    Trace("")
    Trace("No file type provided.")
    return
end
if (ext <> "plx") & (ext <> "nex")
    Trace("")
    Trace("File type must be 'plx' or 'nex' (case sensitive).")
    return
end
ext = "." + ext
Trace("Extension: " + ext)

% Make sure there is at least one file of this type in the folder
if (GetFileCount(rootPath + "\*" + ext) == 0)
    Trace("")
    Trace("No " + ext + " files in this folder.")
    return
end

% Ask the user which burst analysis algorithm they wish to use
algorithm = "Surprise"
result = Dialog(GetActiveDocument(), algorithm, "Which algorithm? (enter 'Max Interval' or 'Surprise')", "string")
if (result <> 1)
    Trace("")
    Trace("No Burst Analysis algorithm provided.")
    return
end
if (algorithm <> "Max Interval") & (algorithm <> "Surprise")
    Trace("")
    Trace("Algorithm must be 'Max Interval' or 'Surprise' (case sensitive).")
    return
end

% Allow the user to set parameters based on the selected analysis algorithm
Trace("Algorithm: " + algorithm)
minSurprise = 4
maxISIToStart = 0.5
minISIToEnd = 0.5
minIBI = 1
minBurstDur = 0.1
minSpikesInBurst = 4
if (algorithm == "Surprise")
    result = Dialog(GetActiveDocument(), minSurprise, "Min Surprise:", "number")
else
    result = Dialog(GetActiveDocument(), maxISIToStart,    "Max ISI To Start Burst",  "number", \
                                         minISIToEnd,      "Min ISI To End Burst",    "number", \
                                         minIBI,           "Min Interburst Interval", "number", \
                                         minBurstDur,      "Min Burst Duration",      "number", \
                                         minSpikesInBurst, "Min Spikes Per Burst",    "number")
    if ((maxISIToStart <= 0) | (minISIToEnd <= 0) | (minIBI <= 0) | (minBurstDur <= 0) | (minSpikesInBurst <= 0))
        Trace("")
        Trace("All Max Interval parameters must be positive.")
        return
    end
end
if (result <> 1)
    Trace("")
    Trace("Burst Analysis using " + algorithm + " algorithm was canceled.")
    return
end

% Process all files in the folder
Trace("")
for recording = 1 to GetFileCount(rootPath + "\*" + ext)
    filePath = GetFileName(recording)
    RunScript("DoBurstAnalysis")
end

% Display success
Trace("")
Trace("All " + NumToStr(GetFileCount(rootPath + "\*" + ext)) + " " + ext + " files in this folder were succesfully burst-analyzed and exported.")
Trace("I hope you remembered to export only Neuron (spike) and Interval data!")
